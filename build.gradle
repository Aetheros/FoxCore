import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

plugins {
  id 'org.spongepowered.plugin' version '0.5.1'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

group 'net.foxdenstudio.sponge.foxcore'
version 'ALPHA-SNAPSHOT'

apply from: project.file("sponge.gradle")


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sponge.plugin {
    id = 'foxcore'
}

minecraft {
    version = "1.8.9-11.15.0.1694"
    runDir = "run"
    mappings = "snapshot_20160109"
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:$spongeVersion"
    compile 'com.flowpowered:flow-math:1.0.1'
    compile 'org.mapdb:mapdb:3.0.0-M4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

/* TODO: This is broken, but was never replacing anything anyway
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}*/

shadowJar {
    classifier = ''
    dependsOn reobfJar
    dependencies {
        include dependency('com.flowpowered:flow-math')
        include dependency('org.mapdb:mapdb')
    }
    relocate 'com.flowpowered.math', 'net.foxdenstudio.shadow.flowmath'
    //relocate 'org.mapdb'
}

task serverJar(type: ShadowJar) {
    classifier = 'server'
    from(sourceSets.main.output) {
        exclude "net/foxdenstudio/sponge/foxcore/mod"
    }
    configurations = [ configurations.runtime ]
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

artifacts {
    archives shadowJar
    archives serverJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
